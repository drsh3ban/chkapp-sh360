// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.2'
        classpath 'com.google.gms:google-services:4.4.4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: "variables.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    
    afterEvaluate { project ->
        if (project.plugins.hasPlugin('java') || project.plugins.hasPlugin('com.android.application') || project.plugins.hasPlugin('com.android.library')) {
            try {
                project.extensions.getByType(org.gradle.api.plugins.JavaPluginExtension).toolchain {
                    languageVersion = JavaLanguageVersion.of(17)
                }
            } catch (Exception e) {
                // Ignore if extension not found
            }
        }

        tasks.withType(JavaCompile).configureEach {
            sourceCompatibility = JavaVersion.VERSION_17
            targetCompatibility = JavaVersion.VERSION_17
        }

        if (project.extensions.findByName('android')) {
            project.android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
        }

        if (project.extensions.findByName('kotlin')) {
            try {
                project.kotlin.jvmToolchain(17)
            } catch (Exception e) {}
        }

        tasks.matching { it.name.contains("Kotlin") && it.hasProperty('kotlinOptions') }.configureEach {
            kotlinOptions {
                jvmTarget = "17"
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
